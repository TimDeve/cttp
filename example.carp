(Debug.sanitize-addresses)

(load "git@github.com:TimDeve/hoquet@v0.1.1")

(load "koi.carp")

(use Koi)

(def my-name @"Carp")
(def my-age @"1337")

(defn css []
 (with Elements
   (style {@"type" @"text/css"}
     [@"
       body {
         color: #EEE;
         background-color: #111;
         font-family: sans-serif;
       }

       a {
        color: rgb(101, 111, 253);
       }

       a:visited {
        color: rgb(192, 158, 255);
       }
     "])))

(defn a-form []
 (with Elements
  (form
   {@"action" @"/submit"
    @"method" @"post"}
   [(input {@"name" @"name"
            @"placeholder" @"Name"})
    (br {})
    (input {@"name" @"age"
            @"placeholder" @"Age"})
    (br {})
    (button {} [@"Submit"])])))

(defn html-template [body-content]
 (with Elements
   (html {}
    [(head {}
      [(title {} [@"Hello Carp"])
       (css)])
     (body {} body-content)])))

(sig root-handler (Fn [Request] Response))
(defn root-handler [req]
 (with Elements
  (Res.send (HttpStatus.Ok)
            @"text/html"
            (html-template [(p {} [@"Hello "
                                   @&my-name
                                   @", you are "
                                   @&my-age
                                   @" years old."])
                            (hr {})
                            (a {@"href" @"/dice"} [@"Roll the dice"])
                            (hr {})
                            (p {} [@"Update your info:"])
                            (a-form)]))))

(sig dice-handler (Fn [Request] Response))
(defn dice-handler [req]
 (with Elements
  (Res.send (HttpStatus.Ok)
            @"text/html"
            (html-template [(p {} [@"You rolled a " (str (Int.random-between 1 7))])]))))

(sig update-name-and-age (Fn [(Map String String)] ()))
(defn update-name-and-age [m]
 (let-do [mss (the (Map String String) m)]
  (set! my-name (Map.get &mss "name"))
  (set! my-age (Map.get &mss "age"))))

(sig post-example-handler (Fn [Request] Response))
(defn post-example-handler [req]
  (do
    (match (Req.params-from-form (Request.body &req))
      (Result.Success q) (update-name-and-age q)
      (Result.Error e) (println* e))
   (Res.found @"/")))

(defn routes []
  [(Route.init (HttpVerb.GET)  @"/"       root-handler)
   (Route.init (HttpVerb.GET)  @"/dice"   dice-handler)
   (Route.init (HttpVerb.POST) @"/submit" post-example-handler)])

(defn main []
  (serve "127.0.0.1" 8090 &(routes)))

