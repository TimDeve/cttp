(load "git@github.com:carpentry-org/http@0.0.1")
(load "git@github.com:carpentry-org/sockets@edbb459fb909c51be907abca753a3ae8ea80f242")
(load "git@github.com:TimDeve/hoquet@v0.1.0")

(use Socket)

(defn hello-html []
 (with Elements
   (html {}
    [(body {}
      [(p {}
        [@"Hello"])
       (br {})
       (p {}
        [@"Carp"])])])))

(defn ok-res [body]
 (Response.init
   200
   @"OK"
   @"HTTP/1.1"
   []
   {@"Server" [@"Carp"]
    @"Content-Type" [@"text/html"]
    @"Content-Length" [(str (length &body))]
    @"Connection" [@"close"]}
   body))

(defn main []
  (Socket.with-server server "127.0.0.1" 8090
    (Socket.while-connection &server client
      (send &client &(let-do [res (Response.str &(ok-res (hello-html)))]
                       (println* &res)
                       res)))))

