(load "git@github.com:TimDeve/pair-spread@v0.1.0")

(load "./todo-template.carp")

(def todos-db [@"Have less things on list" @"Stuff"])

(defmodule TodoDomain
  (defn path-prefix [s] (String.concat &[@"/todos" @s]))

  (sig get-todos-handler (Fn [Req] Res))
  (defn get-todos-handler [req]
    (Res.send (HttpStatus.Ok)
              @"text/html"
              (Template.todos-page-html &todos-db)))

  (sig add-todo-handler (Fn [Req] Res))
  (defn add-todo-handler [req]
    (match (Req.params-from-form (Req.body &req))
      (Result.Success q) (do
                          (push-back! &todos-db (Map.get &q "description"))
                          (Res.found (path-prefix "")))
      (Result.Error e) (bad-request)))

  (sig remove-todo-handler (Fn [Req] Res))
  (defn remove-todo-handler [req]
    (match (Req.params-from-form (Req.body &req))
      (Result.Success q) (do
                          (set!
                            todos-db
                            (Array.remove-nth
                              (unsafe-from (Int.from-string &(Map.get &q "id")))
                              @&todos-db))
                          (Res.found (path-prefix "")))
      (Result.Error e) (bad-request)))

  (defn routes []
    [(Route.init (HttpVerb.GET)  (path-prefix "") get-todos-handler)
     (Route.init (HttpVerb.POST) (path-prefix "") add-todo-handler)
     (Route.init (HttpVerb.POST) (path-prefix "/delete") remove-todo-handler)]))

